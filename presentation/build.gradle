apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

def versionMajor = 4
def versionMinor = 0
def versionPatch = 15

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "br.com.disapps.meucartaotransporte"
        minSdkVersion 16
        targetSdkVersion 27
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode versionMajor * 1000000 + versionMinor * 1000 + versionPatch * 100
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
        buildConfigField 'String', 'GOOGLE_PLAY_PUB_KEY', "\"MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEAqW3dO9Bvfme9nBzVl9tUAKTBFbr6cC20GGmxWFrpcUp5dtdbzdKv7GinkKbMLwe7Ymxz0LupkqMTQmXL3Pc0R0jtYrYpOnrSSYP6i2u3Ln/uMxaOe4D68+nsJHRBylNesGsX61FQrY0PNXyGBoZ5yU6/8F9mPmDpQ00CM+ERVtSc2ZpXyMeBEVobsqpRkbpk3hO/7cxmeHDzhS17C7KNxAcaZrgjfazl0AjXAOjPtXdCYwm+lQnd/5DmqhJwYeoO59gd9gYbjt4orVrDFRMQReJNiKUlHQOr18fIzL+Y/GLE2os0vZhHZ+30kVpkTca5uv6nesTGw8+CpvwuBtCDSwIDAQAB\""
        buildConfigField 'String', 'BILLING_PACKAGE', "\"br.com.disapps.meucartaotransporte\""
        manifestPlaceholders = [onesignal_app_id               : "ed3931c2-93ef-43b4-bfcf-51a5dce79af6",
                                onesignal_google_project_number: "95448377579"]
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix "-dev"
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries = false
        dexInProcess = false
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'publicsuffixes.gz'
        exclude 'META-INF/rxjava.properties'
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies
    implementation project(':domain')
    implementation project(':data')
    implementation 'com.android.support:multidex:1.0.3'
    implementation presentationDependencies.appCompatV7
    implementation presentationDependencies.supportV4
    implementation presentationDependencies.supportDesign
    implementation presentationDependencies.constraintLayout
    implementation presentationDependencies.vectorDrawable
    implementation presentationDependencies.cardView
    implementation presentationDependencies.kotlin
    implementation presentationDependencies.koin
    implementation presentationDependencies.coroutinesAndroid
    implementation presentationDependencies.koinArch
    implementation presentationDependencies.archExtensions
    implementation presentationDependencies.baseRecyclerView
    implementation presentationDependencies.recyclerviewFastscroll
    implementation presentationDependencies.lottie
    implementation presentationDependencies.firebaseCore
    implementation presentationDependencies.firebaseMessaging
    implementation presentationDependencies.firebaseAds
    implementation presentationDependencies.playServicesLocation
    implementation presentationDependencies.playServicesMap
    implementation presentationDependencies.jobDispatcher
    implementation presentationDependencies.customtabs
    implementation presentationDependencies.zxing
    implementation presentationDependencies.zxingAndroid
    implementation presentationDependencies.oneSignal


    kapt presentationDependencies.archCompiler


    // Instrumentation test dependencies
    testImplementation presentationTestDependencies.kotlinJUnit
    androidTestImplementation presentationTestDependencies.junit
    androidTestImplementation presentationTestDependencies.mockito
    androidTestImplementation presentationTestDependencies.mockitoAndroid
    androidTestImplementation(presentationTestDependencies.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.androidRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.androidRules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.espressoIntents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.espressoContrib) {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
}

apply plugin: 'com.google.gms.google-services'
