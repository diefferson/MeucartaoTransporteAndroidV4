apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "br.com.disapps.meucartaotransporte"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode versionMajor * 1000000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            applicationIdSuffix ""
            versionNameSuffix ""
            buildConfigField 'String', 'APPODEAL_APPKEY', "\"83afaed2b8417ec012d70d06673c5cfc69d403fdab1e34a4\""
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField 'String', 'APPODEAL_APPKEY', "\"de34bc5f4d9328ba124fc4320ef780a0158265c00806dce8\""
            testCoverageEnabled true
        }
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        preDexLibraries = false
        dexInProcess = false
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'publicsuffixes.gz'
        exclude 'META-INF/rxjava.properties'
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}


dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies
    implementation project(':domain')
    implementation project(':data')
    implementation 'com.android.support:multidex:1.0.3'
    implementation presentationDependencies.appCompatV7
    implementation presentationDependencies.supportV4
    implementation presentationDependencies.supportDesign
    implementation presentationDependencies.constraintLayout
    implementation presentationDependencies.vectorDrawable
    implementation presentationDependencies.cardView
    implementation presentationDependencies.kotlin
    implementation presentationDependencies.koin
    implementation presentationDependencies.coroutinesAndroid
    implementation presentationDependencies.koinArch
    implementation presentationDependencies.archExtensions
    implementation presentationDependencies.baseRecyclerView
    implementation presentationDependencies.recyclerviewFastscroll
    implementation presentationDependencies.pageIndicatorVersion
    implementation presentationDependencies.lottie
    implementation presentationDependencies.firebaseCore
    implementation presentationDependencies.firebaseMessaging
    implementation presentationDependencies.playServicesLocation
    implementation presentationDependencies.playServicesMap
    implementation presentationDependencies.playServicesAds

    kapt presentationDependencies.archCompiler

    implementation presentationDependencies.customtabs

    implementation(presentationDependencies.fabric) {
        transitive = true
    }

    // Instrumentation test dependencies
    testImplementation presentationTestDependencies.kotlinJUnit
    androidTestImplementation presentationTestDependencies.junit
    androidTestImplementation presentationTestDependencies.mockito
    androidTestImplementation presentationTestDependencies.mockitoAndroid
    androidTestImplementation(presentationTestDependencies.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.androidRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.androidRules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.espressoIntents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(presentationTestDependencies.espressoContrib) {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    
    //ads
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: 'libs/aar', include: ['*.aar'])
}

apply plugin: 'com.google.gms.google-services'
